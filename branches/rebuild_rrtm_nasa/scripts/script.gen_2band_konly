#!/bin/tcsh
#Modified: Jennifer Delamere, Aug 2014 
#Project: RRTM LW REBUILD FOR NASA
#Created: Jennifer Delamere January 1999

#DESCRIPTION:
#  Script to calculate k-distributions for key and minor species;
#  upper and lower atmosphere for RRTM.

#INPUT:
#  DIRNAME ($1): name of the directory you want to create to store all output
#  i.e. script.gen_2band_konly build_gb5
#  INPUT CODE FILENAME ($2): Name of file that contains environment variables to be
#  INPUT PARAM FILENAME ($3): Name of namelist file that describes the bands
#  used in these scripts. 

#Create a directory for the band you are interested in
mkdir $1
echo Create Directory: $1

#Copy files to the directories that need it.
\cp $2 $1/input_codes
\cp $3 $1/input_param

#Source input set of codes; set up lbl,tape3,scriptdirs,etc
source $2

#Now commence work in the new band directory
cd $1

#Create file that contains some information output
echo START TIME: `date` > build_info.txt

echo LBLRTM_VERSION: ${lblver} >> build_info.txt
echo LINEFILE_VERSION: ${t3dir}/TAPE3 >> build_info.txt
echo SCRIPTDIRECTORY: ${scriptdir} >> build_info.txt
echo SCRIPT SVN INFO: >> build_info.txt
echo `svn info ${scriptdir}` >> build_info.txt

#Create a link for TAPE3 for lblrtm_f90 run
\ln ${lblver} lblrtm_f90
\ln -s ${t3dir}/TAPE3 TAPE3
\cp ${t3dir}/TAPE6 tape3_info.txt
\cp ${scriptdir}/PRESSURE.PROFILE .

echo Writing TAPE5s
#Generate TAPE5s for the 5 T(ref) lower/upper atmospheres.
#Includes all absorbers, including the H20 continuum
${exedir}/write_tape5 < input_param

#Generate TAPE5s for the 5 T(ref) lower/upper atmospheres.
#Do NOT include the H20 continuum
${exedir}/write_tape5_nc < input_param

#Generate TAPE5s for T(ref) and T(ref) - 15 for lower/upper atmospheres.
${exedir}/write_tape5_minor < input_param

#LOWER ATMOSPHERE
#Each troposphere run (P > 100mb) has 13 layers.  Generate the 
#k-distributions for 5 runs with T(ref), T(ref) +/- 15 K, 
#T(ref) +/- 30 K.

#Test to see if you are doing case of no or one or two major gases
#by checking the existence of the tape5 files.

if ( -e "tape5-T03-n01") then
    @ jl=1
    echo Two Key Species in Lower Atmosphere
else
    @ jl=9	
    echo One Key Species in Lower Atmosphere
endif

if (-e "tape5nc-T03-n09") then
    echo Starting
else
    echo No Key Species in Lower Atmosphere
    @ jl = 11
endif

# loop over eta
while ($jl < 10)
    @ i=1
# loop over reference temperatures
    while ($i < 6)
	echo eta = 0$jl tref = 0$i
	set tape5nc = tape5nc-T0${i}-n0${jl}
	set tape5od = tape5-T0${i}-n0${jl} 
	set kgfile = KG-T0${i}-n0${jl}
#Run LBLRTM_F90 without the WV continuum and without all absorbers except
#key species
	\cp ${tape5nc} TAPE5
        lblrtm_f90
#LBLRTM_F90 generates ODint_ files for each layer. Move
#these to another filename
	${scriptdir}/script.mv_files mv ODint NCint
#Run LBLRTM_F90 with all the absorbers including continua except WV.
	\rm TAPE5
	\cp ${tape5od} TAPE5
	lblrtm_f90
	${exedir}/kdis_2sort_l < input_param 
#The output of absorption coefficients in file CK_ABS.DAT
#to reference file
        \mv -f CK_ABS.DAT ${kgfile}

	if ($i == 3) then
	    ${scriptdir}/script.mv_files mv ODint ODtmp
	    @ k=1
	    while ($k < 8)
		if (-e "tape5nc-minor0"$k"-T03") then
		echo Calculating Minor Gas ${k}
		@ k1 = 2
		while (${k1} < 4) 
		set tape5minor = tape5nc-minor0${k}-T0${k1}
		set kgminor =  KG-minor0$k-T0${k1}-n0${jl}
		\cp ${tape5minor} TAPE5
		lblrtm_f90
		${scriptdir}/script.mv_files mv ODint NCint
		${scriptdir}/script.mv_files cp ODtmp ODint
 		\rm input_minor
		echo " &igaspar igas_on="$k" iatmtype=1/" > tmp
		cat input_param tmp > input_minor
		\rm tmp
		${exedir}/kdis_2sort_minor < input_minor
	 	\mv -f CK_ABS_MINOR.DAT ${kgminor}		
		@ k1++
		end
		endif
		@ k++
	    end	
	endif

	\rm OD*
        \rm NC*
#Advance counter and run another reference TAPE5 file
	@ i++
    end
    @ jl++
end

echo Starting Upper
# Repeat for UPPER ATMOSPHERE

if ( -e "tape5-T08-n01") then
    @ ju=1
    echo Two Key Species in Upper Atmosphere
else
    @ ju=9
    echo One Key Species in Upper Atmosphere
endif

if (-e "tape5nc-T08-n09") then
    echo Starting
else
    echo No Key Species in Upper Atmosphere
    @ ju = 11
endif


while ($ju < 10)
    @ i=6
    while ($i < 11)
	echo eta = 0$ju tref = 0$i
	if ($i < 10) then
	    set tape5nc = tape5nc-T0${i}-n0${ju}
	    set tape5od = tape5-T0${i}-n0${ju} 
	    set kgfile = KG-T0${i}-n0${ju}
	else
	    set tape5nc = tape5nc-T${i}-n0${ju}
	    set tape5od = tape5-T${i}-n0${ju} 	
	    set kgfile = KG-T${i}-n0${ju}
	endif
        \rm TAPE5
	\cp ${tape5nc} TAPE5
	lblrtm_f90
	${scriptdir}/script.mv_files mv ODint NCint 
	\rm TAPE5
	\cp ${tape5od} TAPE5
	lblrtm_f90
	${exedir}/kdis_2sort_u < input_param
	\mv -f CK_ABS.DAT ${kgfile}

	if ($i == 8) then
	    ${scriptdir}/script.mv_files mv ODint ODtmp
	    @ k=1
	    while ($k < 8)
		if (-e "tape5nc-minor0"$k"-T08") then
		echo Calculating Minor Gas ${k}
		@ k1 = 7
		while (${k1} < 9) 
		set tape5minor = tape5nc-minor0${k}-T0${k1}
		set kgminor =  KG-minor0$k-T0${k1}-n0${ju}
		\cp ${tape5minor} TAPE5
		lblrtm_f90
		${scriptdir}/script.mv_files mv ODint NCint
		\rm TAPE5
		${scriptdir}/script.mv_files cp ODtmp ODint
		\rm input_minor
		echo " &igaspar igas_on="$k" iatmtype=2/" > tmp
		cat input_param tmp > input_minor
		\rm tmp
		${exedir}/kdis_2sort_minor < input_minor
	 	\mv -f CK_ABS_MINOR.DAT ${kgminor}		
		@ k1++
		end
		endif
		@ k++
	    end	
	endif
	\rm OD*
        \rm NC*
	@ i++
    end
    @ ju = $ju + 2
end

echo 'Writing absorption coefficients to file'

#${exedir}/write_data_kminor < input_param
${exedir}/write_data_multiple < input_param

echo Finish Absorption Coefficients

echo FINISH TIME: `date` >> build_info.txt




