#!/bin/tcsh
#Created: Jennifer Delamere January 1999
#INPUT: Type script.gen_2band DIRNAME where DIRNAME is the name of the
#directory you want to create to store all the output.
#NAMELIST NEEDED: Call it 'input_param'

# Set home directory containing source code and scripts
set prrtm_dir = '/project/p1280/cadyp/prrtm'

#Create a directory for the band you are interested in

mkdir $1
echo Create Directory: $1

@ minor_temp_index = $2
@ planck_temp_index = $3

set PLBL = 'plblrtm_v_1.3_linux_pgi_sgl_earth'

#Copy NAMELIST (used by f90 codes) file to the directories that need it.
\cp input_param $1/. 

#Now commence work in the new band directory
cd $1


#Get a copy of TAPE3 for lblrtm_f90 run
\cp ../TAPE3 .
\cp ../PRESSURE.PROFILE .

echo Writing TAPE5s
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
# All upper atmosphere code is beign bypassed for planetary prrtm (for now 7/15/08)
# It will not work as is.
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#Generate TAPE5s for the 14 T(ref) lower/upper atmospheres.
#Includes all absorbers, including the H20 continuum
write_tape5 < input_param

#Generate TAPE5s for the 14 T(ref) lower/upper atmospheres.
#Do NOT include the H20 continuum
write_tape5_nc < input_param

#Generate TAPE5s for T(ref) and T(ref) - 15 for lower/upper atmospheres.
#  Have not worried about this step, as there are no minor gases for band 4
write_tape5_minor < input_param


#LOWER ATMOSPHERE
#Each lower atmosphere run  has 71 layers.  Generate the 
#k-distributions for 14 runs; each run has an isothermal atmosphere
# T=110+n*15, where n ranges from 0 to 13

#Test to see if you are doing case of no or one or two major gases
#by checking the existence of the tape5 files.

# Define number of eta and temp runs
 @ neta=9
 @ ntemp=14


if ( -e "tape5-T06-n01-co2_01") then
    @ jl=1
    echo Two Key Species in Lower Atmosphere
else
    @ jl=$neta
    echo One Key Species in Lower Atmosphere
endif

if (-e "tape5nc-T06-n09-co2_01") then
    echo Starting
else
    echo No Key Species in Lower Atmosphere
    @ jl = $neta+1
endif

# loop over eta
 while ($jl <= $neta)
  @ i=1
   #@ i=10
# loop over reference temperatures
   while ($i <= $ntemp)
   #   while ($i < 10)
	echo eta = 0$jl tref = 0$i
	if ($i < 10) then
	    set tape5nc = tape5nc-T0${i}-n0${jl}-co2_01
	    set tape5od = tape5-T0${i}-n0${jl}-co2_01
	    set kgfile = KG-T0${i}-n0${jl}-co2_01
	else
	    set tape5nc = tape5nc-T${i}-n0${jl}-co2_01
	    set tape5od = tape5-T${i}-n0${jl}-co2_01
	    set kgfile = KG-T${i}-n0${jl}-co2_01
	endif
#Run LBLRTM_F90 without the WV continuum and without all absorbers except
#key species
	\cp ${tape5nc} TAPE5
        $PLBL
#LBLRTM_F90 generates ODint_ files for each layer. Move
#these to another filename
	$prrtm_dir/scripts/script.mv_files 71 ODint_ NCint_
#Run LBLRTM_F90 with all the absorbers including continua except WV.
	\rm TAPE5
	\cp ${tape5od} TAPE5
        $PLBL
         kdis_2sort_l < input_param 
#The output of absorption coefficients in file CK_ABS.DAT
#to reference file
        \mv -f CK_ABS.DAT ${kgfile}
         

	if ($i == $minor_temp_index) then
	    $prrtm_dir/scripts/script.mv_files 71 ODint_ ODtmp_
	    @ k=1
 	    while ($k < 8)
#	    while ($k < 3)
		if (-e "tape5nc-minor0"$k"-T06") then
		echo Calculating Minor Gas ${k}
		@ k1 = $minor_temp_index - 1
		while (${k1} < $minor_temp_index + 1) 
		if (${k1} < 10) then
		   set tape5minor = tape5nc-minor0${k}-T0${k1}
	        else
		   set tape5minor = tape5nc-minor0${k}-T${k1}
                endif

                if ($k1 == $minor_temp_index - 1) then
                    set tfile = 'l'
                else
                    set tfile = 'r'
                endif
		set kgminor =  KG-minor0$k-T${tfile}-n0${jl}
		\cp ${tape5minor} TAPE5
		$PLBL
		$prrtm_dir/scripts/script.mv_files 71 ODint_ NCint_
		$prrtm_dir/scripts/script.cp_files 71 ODtmp_ ODint_
 		\rm input_minor
		echo " &igaspar igas_on="$k" iatmtype=1/" > tmp
		cat input_param tmp > input_minor
		\rm tmp
		kdis_2sort_minor < input_minor
	 	\mv -f CK_ABS_MINOR.DAT ${kgminor}		
		@ k1++
		end
		endif
		@ k++
	    end	
	endif

       @ i++
	\rm OD*
        \rm NC*
#Advance counter and run another reference TAPE5 file
    end
    @ jl++
end

echo 'Writing absorption coefficients to file'

write_data_kminor < input_param
write_data_multiple < input_param

#Generate continuum and planck sort

echo Finish Absorption Coefficients

$prrtm_dir/scripts/script.gen_contin.flux_2013 $planck_temp_index


#Generate the necessary Planck information to be used as
#input to ARRTM. This information is compiled into a file
#called block_planck which is then transfered into ARRTM
#program ASETCOEF.F

echo 'Generating Planck functions for band'
planck_int < input_param

\mv DATA.KG k_gB01.f

#Remove all excess files from the band directory before
#leaving it.
\rm OD*
\rm NC*
\rm TAPE3
\rm TAPE9
\rm TAPE10
\rm TAPE11
\rm TAPE12

#Now go to the ARRTM directory and run the code to
#calculate the fluxes in your band.


echo 'All output in directory '$1
