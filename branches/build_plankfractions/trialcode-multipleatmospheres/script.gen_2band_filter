#!/bin/tcsh
#Created: Jennifer Delamere January 1999
#INPUT: Type script.gen_2band DIRNAME where DIRNAME is the name of the
#directory you want to create to store all the output.
#NAMELIST NEEDED: Call it 'input_param'

#Create a directory for the band you are interested in


mkdir $1
echo Create Directory: $1

#Copy NAMELIST (used by f90 codes) file to the directories that need it.
\cp input_param $1/. 
\cp input_param lbl_f90/.

\cp /stormrc3/jdelamer/flrt_jen/flrt_ancillary/filter_function/$2/$3 lbl_f90/TAPE5.FILTER
\cp /stormrc3/jdelamer/flrt_jen/flrt_ancillary/filter_function/$2/$3 $1/TAPE5.FILTER

#Now commence work in the new band directory
cd $1

#Get a copy of TAPE3 for lblrtm_f90 run
\cp ../lbl_f90/TAPE3 .
\cp ../central_exec_scripts/PRESSURE.PROFILE .

#Set up arrtm_rad directory
mkdir src_arrtm_$1

\cp ../central_exec_scripts/arrtm_rad_src_template/*.f src_arrtm_$1/.
\cp ../central_exec_scripts/arrtm_rad_src_template/INPUT_RRTM_MLS .
\cp ../central_exec_scripts/arrtm_rad_src_template/INPUT_RRTM_SAW .
\cp ../central_exec_scripts/arrtm_rad_src_template/INPUT_RRTM_TRP .
\cp ../central_exec_scripts/arrtm_rad_src_template/make_arrtm_f90 src_arrtm_$1/.

echo Writing TAPE5s
#Generate TAPE5s for the 5 T(ref) lower/upper atmospheres.
#Includes all absorbers, including the H20 continuum
write_tape5 < input_param

#Generate TAPE5s for the 5 T(ref) lower/upper atmospheres.
#Do NOT include the H20 continuum
write_tape5_nc < input_param

#Generate TAPE5s for T(ref) and T(ref) - 15 for lower/upper atmospheres.
write_tape5_minor < input_param

#LOWER ATMOSPHERE
#Each troposphere run (P > 100mb) has 13 layers.  Generate the 
#k-distributions for 5 runs with T(ref), T(ref) +/- 15 K, 
#T(ref) +/- 30 K.

#Test to see if you are doing case of no or one or two major gases
#by checking the existence of the tape5 files.


if ( -e "tape5-T03-n01") then
    @ jl=1
    echo Two Key Species in Lower Atmosphere
else
    @ jl=9	
    echo One Key Species in Lower Atmosphere
endif

if (-e "tape5nc-T03-n09") then
    echo Starting
else
    echo No Key Species in Lower Atmosphere
    @ jl = 11
endif


while ($jl < 10)
    @ i=1
    while ($i < 6)
	echo eta = 0$jl tref = 0$i
	set tape5nc = tape5nc-T0${i}-n0${jl}
	set tape5od = tape5-T0${i}-n0${jl} 
	set kgfile = KG-T0${i}-n0${jl}
#Run LBLRTM_F90 without the WV continuum and without all absorbers except
#key species
	\cp ${tape5nc} TAPE5
        lblrtm_f90
#LBLRTM_F90 generates ODint_ files for each layer. Move
#these to another filename
	script.mv_files 13 ODint_ NCint_
#Run LBLRTM_F90 with all the absorbers including continua except WV.
	\rm TAPE5
	\cp ${tape5od} TAPE5
	lblrtm_f90
	kdis_2sort_l < input_param 
#The output of absorption coefficients in file CK_ABS.DAT
#to reference file
        \mv -f CK_ABS.DAT ${kgfile}

	if ($i == 3) then
	    @ k=1
	    while ($k < 8)
		if (-e "tape5nc-minor0"$k"-T03") then
		echo Calculating Minor Gas ${k}
		@ k1 = 2
		while (${k1} < 4) 
		set tape5minor = tape5nc-minor0${k}-T0${k1}
		set kgminor =  KG-minor0$k-T0${k1}-n0${jl}
		\cp ${tape5minor} TAPE5
		lblrtm_f90
		script.mv_files 13 ODint_ NCint_
		 \cp ${tape5od} TAPE5
		 lblrtm_f90
 		\rm input_minor
		echo " &igaspar igas_on="$k" iatmtype=1/" > tmp
		cat input_param tmp > input_minor
		\rm tmp
		kdis_2sort_minor < input_minor
	 	\mv -f CK_ABS_MINOR.DAT ${kgminor}		
		@ k1++
		end
		endif
		@ k++
	    end	
	endif

	\rm OD*
        \rm NC*
#Advance counter and run another reference TAPE5 file
	@ i++
    end
    @ jl++
end

echo Starting Upper
# Repeat for UPPER ATMOSPHERE



if ( -e "tape5-T08-n01") then
    @ ju=1
    echo Two Key Species in Upper Atmosphere
else
    @ ju=9
    echo One Key Species in Upper Atmosphere
endif

if (-e "tape5nc-T08-n09") then
    echo Starting
else
    echo No Key Species in Upper Atmosphere
    @ ju = 11
endif


while ($ju < 10)
    @ i=6
    while ($i < 11)
	echo eta = 0$ju tref = 0$i
	if ($i < 10) then
	    set tape5nc = tape5nc-T0${i}-n0${ju}
	    set tape5od = tape5-T0${i}-n0${ju} 
	    set kgfile = KG-T0${i}-n0${ju}
	else
	    set tape5nc = tape5nc-T${i}-n0${ju}
	    set tape5od = tape5-T${i}-n0${ju} 	
	    set kgfile = KG-T${i}-n0${ju}
	endif
        \rm TAPE5
	\cp ${tape5nc} TAPE5
	lblrtm_f90
	script.mv_files 47 ODint_ NCint_ 
	\rm TAPE5
	\cp ${tape5od} TAPE5
	lblrtm_f90
	kdis_2sort_u < input_param
	\mv -f CK_ABS.DAT ${kgfile}

	if ($i == 8) then
	    @ k=1
	    while ($k < 8)
		if (-e "tape5nc-minor0"$k"-T08") then
		echo Calculating Minor Gas ${k}
		@ k1 = 7
		while (${k1} < 9) 
		set tape5minor = tape5nc-minor0${k}-T0${k1}
		set kgminor =  KG-minor0$k-T0${k1}-n0${ju}
		\cp ${tape5minor} TAPE5
		lblrtm_f90
		script.mv_files 47 ODint_ NCint_
		\rm TAPE5
		 \cp ${tape5od} TAPE5
		 lblrtm_f90
		\rm input_minor
		echo " &igaspar igas_on="$k" iatmtype=2/" > tmp
		cat input_param tmp > input_minor
		\rm tmp
		kdis_2sort_minor < input_minor
	 	\mv -f CK_ABS_MINOR.DAT ${kgminor}		
		@ k1++
		end
		endif
		@ k++
	    end	
	endif
	\rm OD*
        \rm NC*
	@ i++
    end
    @ ju = $ju + 2
end

echo 'Writing absorption coefficients to file'

write_data_kminor < input_param
write_data_multiple < input_param

#Generate continuum and planck sort

echo Finish Absorption Coefficients

script.gen_contin.rad

\cp kg_cont.f src_arrtm_$1/.
\cp kg_planck.f src_arrtm_$1/.

#Generate the necessary Planck information to be used as
#input to ARRTM. This information is compiled into a file
#called block_planck which is then transfered into ARRTM
#program ASETCOEF.F

echo 'Generating Planck functions for band'
comp_wtavg_planck

\cp avplank_01.f src_arrtm_$1/.
\mv DATA.KG k_gB01.f
\cp k_gB01.f src_arrtm_$1/.
\cp kg_minor.f src_arrtm_$1/.

#Remove all excess files from the band directory before
#leaving it.
\rm OD*
\rm NC*
\rm TAPE3
\rm TAPE9
\rm TAPE10
\rm TAPE11
\rm TAPE12

#Now go to the ARRTM directory and run the code to
#calculate the fluxes in your band.

cd src_arrtm_$1/.

\rm k_g.o
\rm asetcoef.o

echo 'Compiling new ARRTM source code'
gmake -f make_arrtm_f90
cd ..

\rm arrtm_rad
ln -s src_arrtm_$1/arrtm_rad .

foreach atmos (MLS SAW TRP)
   \cp INPUT_RRTM_$atmos INPUT_RRTM
   arrtm_rad < input_param 

#After generating the information for your band, copy it
#to the band directory for safekeeping.

   \mv OUTPUT_RRTM_RADIANCE OUTPUT_ARRTM_$atmos

end
\rm INPUT_RRTM
cd ../lbl_f90

foreach atmos (MLS SAW TRP)
#Generate comparison product using LBLRTM_F90 in flux mode.

   set lc_atmos = `echo ${atmos} | tr "[A-Z]" "[a-z]" `
   \rm TAPE5_${atmos}RD
   \rm TAPE5_${atmos}OD
   \rm RADINIT
   \rm template_${lc_atmos}od

   echo 'Running LBLRTM_F90 for '${atmos}

#This run of LBLRTM_F90 generates the optical depth files for the given atmosphere
	write_tape5_${atmos}RD_filter < input_param

   \cp ../central_exec_scripts/template_${lc_atmos} .
   cat template_${lc_atmos}od template_${lc_atmos} > TAPE5_${atmos}OD
   \cp TAPE5_${atmos}OD TAPE5
   lblrtm_f90

#This run of LBLRTM_F90 generates the radiances at 3 given quadrature angles

   \cp TAPE5_${atmos}RD TAPE5
   lblrtm_f90

#This program sums the radiances into fluxes. It uses the input file RADINIT.

   radsum_filter

#Remove extra files and copy relevant files to the band section.

   \rm template_${lc_atmos}od
   \rm template_${lc_atmos}


   \mv TAPE5_${atmos}OD ../$1/.
   \mv TAPE5_${atmos}RD ../$1/.
   \mv radupdn.dat ../$1/OUTPUT_LBLRTM_${atmos}
   \mv RADINIT ../$1/RADINIT_${atmos}

   cleanup_lbl.rad
end

cd ../$1

echo Generate equivalent blackbody comparison charts

\rm input_btcode
echo " &btinfo" > input_btcode
echo "  file_LBL = 'OUTPUT_LBLRTM_MLS'" >> input_btcode
echo "  file_ARRTM = 'OUTPUT_ARRTM_MLS'">> input_btcode
echo "  outfile = 'OUTPUT_URAD_BTEMP_MLS'">> input_btcode
echo "  nlev = 52 /" >> input_btcode
generate_btemp < input_btcode

\rm input_btcode
echo " &btinfo" > input_btcode
echo "  file_LBL = 'OUTPUT_LBLRTM_SAW'" >> input_btcode
echo "  file_ARRTM = 'OUTPUT_ARRTM_SAW'">> input_btcode
echo "  outfile = 'OUTPUT_URAD_BTEMP_SAW'">> input_btcode
echo "  nlev = 60 /" >> input_btcode
generate_btemp < input_btcode

\rm input_btcode
echo " &btinfo" > input_btcode
echo "  file_LBL = 'OUTPUT_LBLRTM_TRP'" >> input_btcode
echo "  file_ARRTM = 'OUTPUT_ARRTM_TRP'">> input_btcode
echo "  outfile = 'OUTPUT_URAD_BTEMP_TRP'">> input_btcode
echo "  nlev = 61 /" >> input_btcode
generate_btemp < input_btcode





echo 'All output in directory '$1
